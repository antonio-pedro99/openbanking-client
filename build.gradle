buildscript {
    repositories {
        maven { url 'https://arti.tw.ee/artifactory/plugins-release' }
    }
}

plugins {
    id 'java'
    id 'checkstyle'
    id 'pmd'
    id 'com.github.spotbugs' version '2.0.1'
    id 'net.researchgate.release' version '2.6.0'
}

ext.artifactoryUser = "$System.env.ARTIFACTORY_USER"
ext.artifactoryPassword = "$System.env.ARTIFACTORY_PASSWORD"

apply from: 'https://artifacts.tw.ee/artifactory/ivy-release-local/com/transferwise/gradle-scripts/1.0.8/libUpload.gradle'

group = 'com.transferwise'

sourceCompatibility = 12
targetCompatibility = 12

ext {
    libs = [
        'spring': '5.1.11.RELEASE',
        'jackson': '2.10.0',
        'lombok': '1.18.10',
        'junit': '5.4.2',
        'mockito': '3.1.0'
    ]
}

repositories {
    maven { url 'https://arti.tw.ee/artifactory/libs-release' }
    maven { url 'https://arti.tw.ee/artifactory/libs-release-local' }
    maven { url 'https://arti.tw.ee/artifactory/remote-repos' }
}

dependencies {
    implementation("org.springframework:spring-web:${libs.spring}")

    implementation('org.apache.httpcomponents:httpclient:4.5.10')

    implementation("com.fasterxml.jackson.core:jackson-annotations:${libs.jackson}")
    implementation("com.fasterxml.jackson.core:jackson-databind:${libs.jackson}")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jdk8:${libs.jackson}")

    implementation('org.bitbucket.b_c:jose4j:0.7.0')

    compileOnly("org.projectlombok:lombok:${libs.lombok}")
    testCompileOnly("org.projectlombok:lombok:${libs.lombok}")
    annotationProcessor("org.projectlombok:lombok:${libs.lombok}")
    testAnnotationProcessor("org.projectlombok:lombok:${libs.lombok}")

    testImplementation("org.junit.jupiter:junit-jupiter:${libs.junit}")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine:${libs.junit}")

    testImplementation("org.mockito:mockito-core:${libs.mockito}")
    testImplementation("org.mockito:mockito-junit-jupiter:${libs.mockito}")

    testImplementation("org.hamcrest:hamcrest:2.1")
    testImplementation("org.skyscreamer:jsonassert:1.5.0")

    testImplementation("org.springframework:spring-test:${libs.spring}")

    testImplementation("com.transferwise.common:tw-base-utils:1.2.5")
}

jar {
    doFirst {
        manifest {
            attributes("Implementation-Title": project.name, "Implementation-Version": project.version)
        }
    }
}

checkstyle {
    configFile = new File(rootDir, "checkstyle.xml")
    toolVersion = '8.26'
    sourceSets = [project.sourceSets.main]
}

tasks.withType(com.github.spotbugs.SpotBugsTask) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
    excludeFilter file('spotbugs_exclude_filter.xml')
}

pmd {
    incrementalAnalysis = true
    ruleSetFiles = files("custom-pmd-rules.xml")
    ruleSets = []
}

wrapper {
    gradleVersion = '6.0.1'
}

release {
    failOnUnversionedFiles = true
    tagTemplate = '$name-$version'
    tagCommitMessage = "[ci skip] creating tag: "
    newVersionCommitMessage = "[ci skip] new version commit: "
}

afterReleaseBuild.dependsOn publish

test {
    useJUnitPlatform()
}
